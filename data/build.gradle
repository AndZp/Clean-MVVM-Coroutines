apply plugin: 'com.android.library'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion


    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    buildTypes.each {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        def tmdbApiKey = properties.getProperty("tmdb_api_key", "")
        it.buildConfigField 'String', "TMDB_API_KEY", tmdbApiKey
        it.buildConfigField "String", "TMBD_BASE_API", "\"https://api.themoviedb.org/3/\""
        it.resValue 'string', "api_key", tmdbApiKey
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

}

dependencies {
    def dataDependencies = rootProject.ext.dataDependencies
    def dataTestDependencies = rootProject.ext.dataTestDependencies

    implementation project(path: ':domain')

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation dataDependencies.appCompat

    // Kotlin
    implementation dataDependencies.kotlin

    // Coroutines
    implementation dataDependencies.coroutines

    // Moshi
    implementation dataDependencies.moshi
    kapt dataDependencies.moshiCodegen

    // Retrofit2
    implementation dataDependencies.retrofit
    implementation dataDependencies.retrofitMoshiConverter

    // Okhttp3
    implementation dataDependencies.okhttp
    implementation dataDependencies.okhttpLogging

    // Timber log
    implementation dataDependencies.timber

    // test
    testImplementation dataTestDependencies.junit
    testImplementation dataTestDependencies.mockito
    androidTestImplementation dataTestDependencies.testRunner

}
